buildscript {
	repositories {
		mavenCentral()
		gradlePluginPortal()
		maven { url 'https://repo.spring.io/plugins-release' }
	}
}

plugins {
	id 'base'
	id 'project-report'
	id 'idea'
	id 'org.sonarqube' version '2.8'
	id 'org.ajoberstar.grgit' version '4.0.1' apply false
	id 'io.spring.nohttp' version '0.0.5.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE' apply false
	id 'com.jfrog.artifactory' version '4.18.2' apply false
	id 'org.asciidoctor.jvm.pdf' version '3.3.2'
	id 'org.asciidoctor.jvm.gems' version '3.3.2'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

apply plugin: 'io.spring.nohttp'

def gitPresent = new File('.git').exists()

if (gitPresent) {
	apply plugin: 'org.ajoberstar.grgit'
}

description = 'Spring for Apache Pulsar'

ext {
	linkHomepage = 'https://github.com/spring-projects-experimental/spring-pulsar'
	linkIssue = 'https://github.com/spring-projects-experimental/spring-pulsar/issues'
	linkScmUrl = 'https://github.com/spring-projects-experimental/spring-pulsar'
	linkScmConnection = 'https://github.com/spring-projects-experimental/spring-pulsar.git'
	linkScmDevConnection = 'git@github.com:spring-projects-experimental/spring-pulsar.git'
	docResourcesVersion = '0.2.5'

	javadocLinks = [
			'https://docs.oracle.com/en/java/javase/17/docs/api/',
			'https://docs.spring.io/spring-framework/docs/current/javadoc-api/'
	] as String[]

	if (gitPresent) {
		modifiedFiles =
				files(grgit.status().unstaged.modified).filter{ f -> f.name.endsWith('.java') }
	}

	assertjVersion = '3.22.0'
	awaitilityVersion = '4.2.0'
	googleJsr305Version = '3.0.2'
	hamcrestVersion = '2.2'
	hibernateValidationVersion = '7.0.4.Final'
	jacksonBomVersion = '2.13.3'
	jaywayJsonPathVersion = '2.6.0'
	junitJupiterVersion = '5.8.2'
	pulsarVersion = '2.10.0'
	log4jVersion = '2.17.2'
	mockitoVersion = '4.5.1'
	reactorVersion = '2020.0.17'
	springBootVersion = '3.0.0-SNAPSHOT' // docs module
	springRetryVersion = '1.3.3'
	springVersion = '6.0.0-SNAPSHOT'
	caffeineVersion = '3.1.1'
	idPrefix = 'pulsar'
}

nohttp {
	source.include '**/src/**'
	source.exclude '**/*.gif', '**/*.ks'
}

allprojects {
	group = 'org.springframework.pulsar'

	apply plugin: 'io.spring.dependency-management'

	dependencyManagement {
		resolutionStrategy {
			cacheChangingModulesFor 0, 'seconds'
		}
		applyMavenExclusions = false
		generatedPomCustomization {
			enabled = false
		}

		imports {
			mavenBom "com.fasterxml.jackson:jackson-bom:$jacksonBomVersion"
			mavenBom "org.junit:junit-bom:$junitJupiterVersion"
			mavenBom "org.springframework:spring-framework-bom:$springVersion"
			mavenBom "io.projectreactor:reactor-bom:$reactorVersion"
			mavenBom "org.apache.logging.log4j:log4j-bom:$log4jVersion"
		}
	}

	repositories {
		mavenCentral()
		maven { url 'https://repo.spring.io/release' }
		maven { url 'https://repo.spring.io/milestone' }
		if (version.endsWith('SNAPSHOT')) {
			maven { url 'https://repo.spring.io/snapshot' }
		}
//		maven { url 'https://repository.apache.org/content/groups/staging/' }
	}

}

subprojects { subproject ->
	apply plugin: 'java-library'
	apply plugin: 'java'
	apply from: "${rootProject.projectDir}/gradle/publish-artifactory.gradle"
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'jacoco'
	apply plugin: 'checkstyle'

	java {
		withJavadocJar()
		withSourcesJar()
		registerFeature('optional') {
			usingSourceSet(sourceSets.main)
		}
		registerFeature('provided') {
			usingSourceSet(sourceSets.main)
		}
	}

	compileJava {
		sourceCompatibility = 17
		targetCompatibility = 17
	}

	compileTestJava {
		sourceCompatibility = 17
		options.encoding = 'UTF-8'
	}

	eclipse.project.natures += 'org.springframework.ide.eclipse.core.springnature'

	jacoco {
		toolVersion = '0.8.7'
	}

	// dependencies that are common across all java projects
	dependencies {
		implementation "com.google.code.findbugs:jsr305:$googleJsr305Version"
		testImplementation 'org.junit.jupiter:junit-jupiter-api'
		testImplementation 'org.junit.jupiter:junit-jupiter-params'
		testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

		// To avoid compiler warnings about @API annotations in JUnit code
		testCompileOnly 'org.apiguardian:apiguardian-api:1.0.0'

		testRuntimeOnly 'org.apache.logging.log4j:log4j-core'
		testRuntimeOnly 'org.apache.logging.log4j:log4j-jcl'

		testImplementation("org.awaitility:awaitility:$awaitilityVersion") {
			exclude group: 'org.hamcrest'
		}
		testImplementation "org.hamcrest:hamcrest-core:$hamcrestVersion"
		optionalApi "org.assertj:assertj-core:$assertjVersion"

		testImplementation "org.testcontainers:pulsar:1.17.2"

		testImplementation "org.springframework:spring-test"

	}

	// enable all compiler warnings; individual projects may customize further
	[compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint:all,-options']

	test {
		testLogging {
			events "skipped", "failed"
			showStandardStreams = project.hasProperty("showStandardStreams") ?: false
			showExceptions = true
			showStackTraces = true
			exceptionFormat = 'full'
		}

		maxHeapSize = '1536m'
//		jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=127.0.0.1:8111'
		jacoco {
			destinationFile = file("$buildDir/jacoco.exec")
		}
		useJUnitPlatform()

		if (System.properties['sonar.host.url']) {
			finalizedBy jacocoTestReport
		}
	}

	checkstyle {
		configDirectory.set(rootProject.file("src/checkstyle"))
		toolVersion = '9.0'
	}

	jacocoTestReport {
		reports {
			xml.enabled true
			csv.enabled false
			html.enabled false
			xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
		}
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				suppressAllPomMetadataWarnings()
				from components.java
				pom.withXml {
					def pomDeps = asNode().dependencies.first()
					subproject.configurations.providedImplementation.allDependencies.each { dep ->
						pomDeps.remove(pomDeps.'*'.find { it.artifactId.text() == dep.name })
						pomDeps.appendNode('dependency').with {
							it.appendNode('groupId', dep.group)
							it.appendNode('artifactId', dep.name)
							it.appendNode('version', dep.version)
							it.appendNode('scope', 'provided')
						}
					}
				}
			}
		}
	}

	task updateCopyrights {
		onlyIf { gitPresent && !System.getenv('GITHUB_ACTION') }
		if (gitPresent) {
			inputs.files(modifiedFiles.filter { f -> f.path.contains(subproject.name) })
		}
		outputs.dir('build')

		doLast {
			def now = Calendar.instance.get(Calendar.YEAR) as String
			inputs.files.each { file ->
				def line
				file.withReader { reader ->
					while (line = reader.readLine()) {
						def matcher = line =~ /Copyright (20\d\d)-?(20\d\d)?/
						if (matcher.count) {
							def beginningYear = matcher[0][1]
							if (now != beginningYear && now != matcher[0][2]) {
								def years = "$beginningYear-$now"
								def sourceCode = file.text
								sourceCode = sourceCode.replaceFirst(/20\d\d(-20\d\d)?/, years)
								file.write(sourceCode)
								println "Copyright updated for file: $file"
							}
							break
						}
					}
				}
			}
		}
	}


	jar {
		manifest {
			attributes(
					'Implementation-Version': archiveVersion,
					'Created-By': "JDK ${System.properties['java.version']} (${System.properties['java.specification.vendor']})",
					'Implementation-Title': subproject.name,
					'Implementation-Vendor-Id': subproject.group,
					'Implementation-Vendor': 'Pivotal Software, Inc.',
					'Implementation-URL': linkHomepage,
					'Automatic-Module-Name': subproject.name.replace('-', '.')  // for Jigsaw
			)
		}

		from("${rootProject.projectDir}/src/dist") {
			include 'license.txt'
			include 'notice.txt'
			into 'META-INF'
			expand(copyright: new Date().format('yyyy'), version: project.version)
		}
		from("${rootProject.projectDir}") {
			include 'LICENSE.txt'
			into 'META-INF'
		}
	}

	tasks.withType(Javadoc) {
		options.addBooleanOption('Xdoclint:syntax', true) // only check syntax with doclint
		options.addBooleanOption('Werror', true) // fail build on Javadoc warnings
	}

}

project ('spring-pulsar') {
	description = 'Spring Pulsar Support'

	dependencies {
		api 'org.springframework:spring-context'
		api 'org.springframework:spring-messaging'
		api 'org.springframework:spring-tx'
		api ("org.springframework.retry:spring-retry:$springRetryVersion") {
			exclude group: 'org.springframework'
		}
		api "org.apache.pulsar:pulsar-client:$pulsarVersion"
		api "org.apache.pulsar:pulsar-client-admin:$pulsarVersion"
		api "org.apache.pulsar:pulsar-client-admin-api:$pulsarVersion"

		api "com.github.ben-manes.caffeine:caffeine:$caffeineVersion"

		optionalApi 'com.fasterxml.jackson.core:jackson-core'
		optionalApi 'com.fasterxml.jackson.core:jackson-databind'
		optionalApi 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
		optionalApi 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
		optionalApi 'com.fasterxml.jackson.datatype:jackson-datatype-joda'
		optionalApi ('com.fasterxml.jackson.module:jackson-module-kotlin') {
			exclude group: 'org.jetbrains.kotlin'
		}

		optionalApi "com.jayway.jsonpath:json-path:$jaywayJsonPathVersion"

		optionalApi 'io.projectreactor:reactor-core'

		testImplementation 'io.projectreactor:reactor-test'
		testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
		testImplementation "org.hibernate.validator:hibernate-validator:$hibernateValidationVersion"
	}
}

project ('spring-pulsar-boot-autoconfigure') {
	description = 'Spring Boot Pulsar Autoconfiguration'

	dependencies {
		annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:$springBootVersion"

		api "org.springframework.boot:spring-boot:$springBootVersion"
		api "org.springframework.boot:spring-boot-autoconfigure:$springBootVersion"
		api "org.springframework.boot:spring-boot-starter:$springBootVersion"
		api "org.springframework.boot:spring-boot-starter-validation:$springBootVersion"

		api project (':spring-pulsar')

		testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
	}
}

project ('spring-pulsar-docs') {
	description = 'Spring Pulsar Code Snippets For Docs'

	dependencies {
		api "org.springframework.boot:spring-boot-starter:$springBootVersion"
		api project (':spring-pulsar')
		optionalApi 'com.fasterxml.jackson.core:jackson-core'
		optionalApi 'com.fasterxml.jackson.core:jackson-databind'
	}
}

project ('spring-pulsar-sample-apps') {
	description = 'Spring Pulsar Sample Applications'

	dependencies {
		api project (':spring-pulsar-boot-autoconfigure')
	}
}

sonarqube {
	properties {
		property 'sonar.links.homepage', linkHomepage
		property 'sonar.links.ci', linkCi
		property 'sonar.links.issue', linkIssue
		property 'sonar.links.scm', linkScmUrl
		property 'sonar.links.scm_dev', linkScmDevConnection
	}
}

// skip publishing the root module
artifactoryPublish.skip = true

if (project.hasProperty('artifactoryUsername')) {
	artifactory {
		contextUrl = 'https://repo.spring.io'
		publish {
			repository {
				repoKey = 'libs-snapshot-local'
				username = "${artifactoryUsername}"
				password = "${artifactoryPassword}"
			}
			defaults {
				publications('mavenJava')
			}
		}
	}
}


configurations {
	docs
}



dependencies {
	docs "io.spring.docresources:spring-doc-resources:${docResourcesVersion}@zip"
}

task prepareAsciidocBuild(type: Sync) {
	dependsOn configurations.docs
	from {
		configurations.docs.collect { zipTree(it) }
	}
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	from 'spring-pulsar-docs/src/main/asciidoc/'
	into "$buildDir/asciidoc"
}

asciidoctorPdf {
	dependsOn prepareAsciidocBuild
	baseDirFollowsSourceFile()
	configurations 'asciidoctorExt'

	asciidoctorj {
		sourceDir "$buildDir/asciidoc"
		inputs.dir(sourceDir)
		sources {
			include 'index.adoc'
		}
		options doctype: 'book'
		attributes 'icons': 'font',
				'sectanchors': '',
				'sectnums': '',
				'toc': '',
				'source-highlighter' : 'coderay',
				revnumber: project.version,
				'project-version': project.version
	}
}

asciidoctorj {
	version = '2.4.1'
	// fatalWarnings ".*"
	options doctype: 'book', eruby: 'erubis'
	attributes([
			icons: 'font',
			idprefix: '',
			idseparator: '-',
			docinfo: 'shared',
			revnumber: project.version,
			sectanchors: '',
			sectnums: '',
			'source-highlighter': 'highlight.js',
			highlightjsdir: 'js/highlight',
			'highlightjs-theme': 'googlecode',
			stylesdir: 'css/',
			stylesheet: 'stylesheet.css',
			'spring-version': project.version,
			'project-version': project.version
	])
}

asciidoctor {
	dependsOn asciidoctorPdf
	baseDirFollowsSourceFile()
	configurations 'asciidoctorExt'
	sourceDir = file("$buildDir/asciidoc")
	sources {
		include '*.adoc'
	}
	resources {
		from(sourceDir) {
			include 'images/*', 'css/**', 'js/**'
		}
	}

}

task reference(dependsOn: asciidoctor) {
	group = 'Documentation'
	description = 'Generate the reference documentation'
}

reference.onlyIf { "$System.env.NO_REFERENCE_TASK" != 'true' || project.hasProperty('ignoreEnvToStopReference') }

task api(type: Javadoc) {
	group = 'Documentation'
	description = 'Generates aggregated Javadoc API documentation.'
	title = "${rootProject.description} ${version} API"
	options {
		encoding = 'UTF-8'
		memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
		author = true
		header = rootProject.description
		use = true
		overview = 'src/api/overview.html'
		splitIndex = true
		links(project.ext.javadocLinks)
		addBooleanOption('Xdoclint:syntax', true) // only check syntax with doclint
	}

	source subprojects.collect { project ->
		project.sourceSets.main.allJava
	}

	classpath = files(subprojects.collect { project ->
		project.sourceSets.main.compileClasspath
	})
	destinationDir = new File(buildDir, 'api')
}

task docsZip(type: Zip, dependsOn: [reference]) {
	group = 'Distribution'
	archiveClassifier = 'docs'
	description = "Builds -${archiveClassifier} archive containing api and reference " +
			"for deployment at static.spring.io/spring-pulsar/docs."

	from('src/dist') {
		include 'changelog.txt'
	}

	from(api) {
		into 'api'
	}

	from ('build/docs/asciidoc') {
		into 'reference/html'
	}

	from ('build/docs/asciidocPdf') {
		include 'index.pdf'
		rename 'index.pdf', 'spring-pulsar-reference.pdf'
		into 'reference/pdf'
	}
}

task distZip(type: Zip, dependsOn: [docsZip]) { //, schemaZip]) {
	group = 'Distribution'
	archiveClassifier = 'dist'
	description = "Builds -${archiveClassifier} archive, containing all jars and docs, " +
			"suitable for community download page."

	ext.baseDir = "${project.name}-${project.version}"

	from('src/dist') {
		include 'readme.txt'
		include 'notice.txt'
		into "${baseDir}"
	}

	from("$project.rootDir") {
		include 'LICENSE.txt'
		into "${baseDir}"
	}

	from(zipTree(docsZip.archivePath)) {
		into "${baseDir}/docs"
	}

	subprojects.each { subproject ->
		into ("${baseDir}/libs") {
			from subproject.jar
			from subproject.sourcesJar
			from subproject.javadocJar
		}
	}
}

artifacts {
	archives distZip
	archives docsZip
}

task dist(dependsOn: assemble) {
	group = 'Distribution'
	description = 'Builds -dist, -docs distribution archives.'
}

apply from: "${rootProject.projectDir}/publish-maven.gradle"

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact docsZip
			artifact distZip
		}
	}
}

apply from: "${rootDir}/gradle/docs.gradle"

