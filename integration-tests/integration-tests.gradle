plugins {
	id 'org.springframework.pulsar.spring-module'
	id "de.undercouch.download" version "5.3.0"
	id 'spring-pulsar.integration-test-conventions'
}

description = 'Spring Pulsar Integration Tests'

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
	intTestImplementation project(':spring-pulsar-test')
	intTestRuntimeOnly 'ch.qos.logback:logback-classic'
	intTestRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	intTestImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
	intTestImplementation "org.springframework.boot:spring-boot-starter-amqp:$springBootVersion"
	intTestImplementation "org.springframework.pulsar:spring-pulsar-spring-boot-starter:$springPulsarStarterVersion"
	intTestImplementation "org.springframework.pulsar:spring-pulsar-reactive-spring-boot-starter:$springPulsarStarterVersion"
	intTestImplementation 'org.testcontainers:junit-jupiter'
	intTestImplementation 'org.testcontainers:pulsar'
	intTestImplementation 'org.testcontainers:rabbitmq'
}

test {
	testLogging.showStandardStreams = true
}

integrationTest {
	maxHeapSize '2048m'
}

task downloadRabbitConnector {
	onlyIf {
		System.getProperty("downloadRabbitConnector") == "true"
	}
	doLast {
		try {
			download.run {
				println "Downloading Rabbit connector to 'src/intTest/resources/connectors/' (one time only if not already downloaded)..."
				src 'https://archive.apache.org/dist/pulsar/pulsar-2.10.2/connectors/pulsar-io-rabbitmq-2.10.2.nar'
				dest "$buildDir/../src/intTest/resources/connectors/pulsar-io-rabbitmq-2.10.2.nar"
				overwrite false
			}
		} catch (Exception ex) {
			println "Failed to download rabbit connector: $ex"
		}
	}
}

project.afterEvaluate {
	integrationTest.dependsOn downloadRabbitConnector
}

project.afterEvaluate {
	project.tasks.publishArtifacts.enabled(false)
	project.tasks.artifactoryPublish.enabled(false)
	project.tasks.publishToOssrh.enabled(false)
	project.tasks.publishMavenJavaPublicationToOssrhRepository.enabled(false)
	project.tasks.publishAllPublicationsToOssrhRepository.enabled(false)
}
