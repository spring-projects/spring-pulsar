plugins {
	id 'org.springframework.pulsar.spring-module'
	id 'org.springframework.pulsar.configuration-properties'
	id "de.undercouch.download" version "5.3.0"
}

description = 'Spring Pulsar Spring Boot Auto-configuration'

dependencies {
	annotationProcessor 'org.springframework.boot:spring-boot-autoconfigure-processor'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	optional project (':spring-pulsar')
	optional project (':spring-pulsar-reactive')
	optional 'org.apache.pulsar:pulsar-client-reactive-producer-cache-caffeine'
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'com.google.code.findbugs:jsr305'

	testImplementation project(':spring-pulsar-test')
	testRuntimeOnly 'org.apache.logging.log4j:log4j-core'
	testRuntimeOnly 'org.apache.logging.log4j:log4j-jcl'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:pulsar'

	// used by PulsarFunctionTests
	testImplementation 'org.testcontainers:rabbitmq'
	testImplementation 'org.springframework.boot:spring-boot-starter-amqp'
}

test {
	testLogging.showStandardStreams = true
}

integrationTest {
	maxHeapSize '2048m'
}

task downloadRabbitConnector {
	onlyIf {
		System.getProperty("downloadRabbitConnector") == "true"
	}
	doLast {
		try {
			download.run {
				println "Downloading Rabbit connector to 'src/test/resources/connectors/' (one time only if not already downloaded)..."
				src 'https://archive.apache.org/dist/pulsar/pulsar-2.11.0/connectors/pulsar-io-rabbitmq-2.11.0.nar'
				dest "$buildDir/../src/test/resources/connectors/pulsar-io-rabbitmq-2.11.0.nar"
				overwrite false
			}
		} catch (Exception ex) {
			println "Failed to download rabbit connector: $ex"
		}
	}
}

project.afterEvaluate {
	integrationTest.dependsOn downloadRabbitConnector
}
