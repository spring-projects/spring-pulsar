plugins {
	id 'java'
	alias(libs.plugins.spring.boot)
	alias(libs.plugins.spring.dep.mgmt)
}

description = 'Spring Pulsar Sample Application (Reader)'

repositories { 
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

def versionCatalog = extensions.getByType(VersionCatalogsExtension).named("libs")
def pulsarVersion = versionCatalog.findVersion("pulsar").orElseThrow().displayName

ext['spring-pulsar.version'] = "${project.version}"
ext['pulsar.version'] = "${pulsarVersion}"

dependencies {
	implementation "org.springframework.boot:spring-boot-starter-pulsar"
}

test {
	useJUnitPlatform()
	testLogging.showStandardStreams = true
	outputs.upToDateWhen { false }
}

bootRun {
	jvmArgs = [
			"--add-opens", "java.base/java.lang=ALL-UNNAMED",
			"--add-opens", "java.base/java.util=ALL-UNNAMED",
			"--add-opens", "java.base/sun.net=ALL-UNNAMED"
	]
}
