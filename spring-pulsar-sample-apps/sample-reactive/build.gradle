plugins {
	id 'java'
	alias(libs.plugins.spring.boot)
	alias(libs.plugins.spring.dep.mgmt)
}

description = 'Reactive Spring Pulsar Sample Application'

repositories { 
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

def versionCatalog = extensions.getByType(VersionCatalogsExtension).named("libs")
def pulsarVersion = versionCatalog.findVersion("pulsar").orElseThrow().displayName
def pulsarReactiveVersion = versionCatalog.findVersion("pulsar-reactive").orElseThrow().displayName

ext['spring-pulsar.version'] = "${project.property('version.samples')}"
ext['pulsar.version'] = "${pulsarVersion}"
ext['pulsar-reactive.version'] = "${pulsarReactiveVersion}"

dependencies {
	implementation "org.springframework.boot:spring-boot-starter-pulsar-reactive"
	developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
	testImplementation project(':spring-pulsar-test')
	testRuntimeOnly 'ch.qos.logback:logback-classic'
	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation "org.springframework.boot:spring-boot-testcontainers"
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:pulsar'
}

test {
	useJUnitPlatform()
	testLogging.showStandardStreams = true
	outputs.upToDateWhen { false }
}

bootRun {
	jvmArgs = [
			"--add-opens", "java.base/java.lang=ALL-UNNAMED",
			"--add-opens", "java.base/java.util=ALL-UNNAMED",
			"--add-opens", "java.base/sun.net=ALL-UNNAMED"
	]
	// when run from command line, path must be set relative to module dir
	systemProperty 'spring.docker.compose.file', 'compose.yaml'
}
