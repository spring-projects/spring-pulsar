settingsEvaluated { settings ->
	settings.pluginManagement {
		repositories {
			mavenCentral()
			gradlePluginPortal()
			maven { url "https://repo.spring.io/milestone" }
			maven { url "https://repo.spring.io/snapshot" }
		}
		resolutionStrategy {
			eachPlugin {
				if (requested.id.id == "org.springframework.boot") {
					useVersion("$rootProject.springBootVersion")
				}
			}
		}
	}
}

allprojects {
	configurations.all {
		resolutionStrategy {
			force "org.springframework.pulsar:spring-pulsar:$springPulsarVersion"
			force "org.springframework.pulsar:spring-pulsar-cache-provider:$springPulsarVersion"
			force "org.springframework.pulsar:spring-pulsar-cache-provider-caffeine:$springPulsarVersion"
			force "org.springframework.pulsar:spring-pulsar-reactive:$springPulsarVersion"
			force "org.springframework.pulsar:spring-pulsar-dependencies:$springPulsarVersion"
		}
	}
	repositories {
		exclusiveContent {
			forRepository {
				maven {
					url "file://$localRepositoryPath"
				}
			}
			filter {
				includeVersion "org.springframework.pulsar", "spring-pulsar", "$springPulsarVersion"
				includeVersion "org.springframework.pulsar", "spring-pulsar-cache-provider", "$springPulsarVersion"
				includeVersion "org.springframework.pulsar", "spring-pulsar-cache-provider-caffeine", "$springPulsarVersion"
				includeVersion "org.springframework.pulsar", "spring-pulsar-reactive", "$springPulsarVersion"
				includeVersion "org.springframework.pulsar", "spring-pulsar-dependencies", "$springPulsarVersion"
			}
		}
	}
}
