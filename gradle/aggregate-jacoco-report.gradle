apply plugin: 'jacoco'

jacoco {
	toolVersion libs.versions.jacoco.get()
}

tasks.withType(Test) {
	jacoco.includeNoLocationClasses = true
}

project.afterEvaluate {

	tasks.create(name: 'aggregateJacocoTestReport', type: JacocoReport) {

		dependsOn(
			':spring-pulsar:compileJava',
			':spring-pulsar-cache-provider:compileJava',
			':spring-pulsar-cache-provider:test',
			':spring-pulsar-cache-provider-caffeine:compileJava')


		group = 'verification'
		description = 'Generates aggregate code coverage report for all projects test tasks'

		reports {
			html.required = true
			xml.required = true
		}

		def excludes = [
				'**/pulsar/**/aot/**'
		]

		def modulesToAggregate = subprojects.findAll { proj ->
			def isSampleApp = proj.getParent().getPath().contains('spring-pulsar-sample-apps')
			def isTestModule = proj.getName() == 'spring-pulsar-test'
			def path = "${proj.projectDir}/src/main/java"
			!(isSampleApp) && !(isTestModule) && (new File(path)).exists()
		}

		def classes = modulesToAggregate.collect {
			proj -> "${proj.buildDir}/classes/java/main"
		}.collect {
			path -> fileTree(dir: path, excludes: excludes)
		}
		classDirectories.from = files(classes)

		def sources = modulesToAggregate.collect {
			proj -> "${proj.projectDir}/src/main/java"
		}.flatten()
		sourceDirectories.from = files(sources)

		def executions = modulesToAggregate.findAll { proj ->
			def path = "${proj.buildDir}/jacoco/test.exec"
			(new File(path)).exists()
		}.collect {
			proj -> "${proj.buildDir}/jacoco/test.exec"
		}
		executionData.from = files(executions)
	}

	tasks.named('check') {
		dependsOn tasks.named('aggregateJacocoTestReport', JacocoReport)
	}
}
