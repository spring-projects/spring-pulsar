plugins {
	id "java-gradle-plugin"
	id "java"
	id "groovy"
}

repositories {
	gradlePluginPortal()
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	if (version.endsWith('SNAPSHOT')) {
		maven { url 'https://repo.spring.io/snapshot' }
	}
	jcenter()
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

sourceSets {
	main {
		java { srcDirs = [] }
		groovy { srcDirs += ['src/main/java'] }
	}
}

configurations {
	implementation {
		exclude module: 'groovy-all'
	}
}

ext {
	springBootVersion = '3.0.0'
}

dependencies {
	implementation localGroovy()
	implementation 'commons-codec:commons-codec'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'io.github.gradle-nexus:publish-plugin:1.1.0'
	implementation("io.spring.javaformat:spring-javaformat-gradle-plugin:0.0.34")
	implementation 'io.spring.nohttp:nohttp-gradle:0.0.10'
	implementation "org.apache.maven:maven-embedder:3.6.2"
	implementation "org.asciidoctor:asciidoctor-gradle-jvm:3.3.2"
	implementation 'org.codehaus.groovy:groovy-all:2.5.17'
	implementation 'org.jfrog.buildinfo:build-info-extractor-gradle:4.29.0'

	implementation "org.gradle:test-retry-gradle-plugin:1.4.0"
	implementation 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7.1'
	implementation(platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))
	implementation 'org.springframework:spring-core'
	implementation 'org.springframework:spring-web'

	testImplementation 'org.assertj:assertj-core'
	testImplementation 'org.apache.logging.log4j:log4j-core'
	testImplementation 'org.junit.jupiter:junit-jupiter'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test', Test).configure {
	onlyIf { !project.hasProperty("buildSrc.skipTests") }
	useJUnitPlatform()
	jvmArgs(
			'--add-opens', 'java.base/java.lang=ALL-UNNAMED',
			'--add-opens', 'java.base/java.util=ALL-UNNAMED'
	)
}

gradlePlugin {
	plugins {
		configurationPropertiesPlugin {
			id = "org.springframework.pulsar.configuration-properties"
			implementationClass = 'org.springframework.pulsar.gradle.docs.configprops.ConfigurationPropertiesPlugin'
		}
		jacocoConventionsPlugin {
			id = "org.springframework.pulsar.jacoco"
			implementationClass = "org.springframework.pulsar.gradle.JacocoConventionsPlugin"
		}
		optionalDependenciesPlugin {
			id = "org.springframework.pulsar.optional-dependencies"
			implementationClass = "org.springframework.boot.gradle.optional.OptionalDependenciesPlugin"
		}
		rootProjectPlugin {
			id = "org.springframework.pulsar.root-project"
			implementationClass = "org.springframework.pulsar.gradle.RootProjectPlugin"
		}
		sonarQubeConventionsPlugin {
			id = "org.springframework.pulsar.sonarqube"
			implementationClass = "org.springframework.pulsar.gradle.SonarQubeConventionsPlugin"
		}
		springDocsModulePlugin {
			id = "org.springframework.pulsar.spring-docs-module"
			implementationClass = "org.springframework.pulsar.gradle.SpringDocsModulePlugin"
		}
		springModulePlugin {
			id = "org.springframework.pulsar.spring-module"
			implementationClass = "org.springframework.pulsar.gradle.SpringModulePlugin"
		}
		updateProjectVersion {
			id = "org.springframework.pulsar.update-version"
			implementationClass = "org.springframework.pulsar.gradle.versions.UpdateProjectVersionPlugin"
		}
		// groovy plugins
		artifactoryPlugin {
			id = "io.spring.convention.artfiactory"
			implementationClass = "io.spring.gradle.convention.ArtifactoryPlugin"
		}
		repositoryConventionPlugin {
			id = "io.spring.convention.repository"
			implementationClass = "io.spring.gradle.convention.RepositoryConventionPlugin"
		}
	}
}
