pluginManagement {
	repositories {
		mavenCentral()
		gradlePluginPortal()
		maven { url "https://repo.spring.io/release" }
		maven { url "https://repo.spring.io/snapshot" }
	}
}

plugins {
	id 'com.gradle.develocity' version '3.17.2'
	id 'io.spring.ge.conventions' version '0.0.17' apply false
}

startParameter.noBuildScan = startParameter.taskNames.contains('format')

apply plugin: 'io.spring.ge.conventions'

include 'spring-pulsar'
include 'spring-pulsar-bom'
include 'spring-pulsar-cache-provider'
include 'spring-pulsar-cache-provider-caffeine'
include 'spring-pulsar-reactive'
include 'spring-pulsar-dependencies'
include 'spring-pulsar-sample-apps:sample-imperative-produce-consume'
include 'spring-pulsar-sample-apps:sample-failover-custom-router'
include 'spring-pulsar-sample-apps:sample-pulsar-functions:sample-signup-app'
include 'spring-pulsar-sample-apps:sample-pulsar-functions:sample-signup-function'
include 'spring-pulsar-sample-apps:sample-reactive'
include 'spring-pulsar-sample-apps:sample-pulsar-binder'
include 'spring-pulsar-sample-apps:sample-pulsar-reader'
include 'spring-pulsar-docs'
include 'spring-pulsar-test'
include 'integration-tests'

rootProject.name = "spring-pulsar-dist"

rootProject.children.findAll { !it.name.startsWith('sample-') }
		.each {project -> project.buildFileName = "${project.name}.gradle" }

settings.gradle.projectsLoaded {
	develocity {
		buildScan {
			publishing.onlyIf { !it.buildResult.failures.empty }
			termsOfUseUrl = 'https://gradle.com/terms-of-service'
			termsOfUseAgree = 'yes'
		}
	}
}
